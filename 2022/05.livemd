# Day 5: Supply Stacks

## Part 1

The expedition can depart as soon as the final supplies have been unloaded from the ships. Supplies are stored in stacks of marked crates, but because the needed supplies are buried under many other crates, the crates need to be rearranged.

The ship has a giant cargo crane capable of moving crates between stacks. To ensure none of the crates get crushed or fall over, the crane operator will rearrange them in a series of carefully-planned steps. After the crates are rearranged, the desired crates will be at the top of each stack.

The Elves don't want to interrupt the crane operator during this delicate procedure, but they forgot to ask her which crate will end up where, and they want to be ready to unload them as soon as possible so they can embark.

They do, however, have a drawing of the starting stacks of crates and the rearrangement procedure (your puzzle input). For example:

```
    [D]    
[N] [C]    
[Z] [M] [P]
 1   2   3 

move 1 from 2 to 1
move 3 from 1 to 3
move 2 from 2 to 1
move 1 from 1 to 2
```

In this example, there are three stacks of crates. Stack 1 contains two crates: crate `Z` is on the bottom, and crate `N` is on top. Stack 2 contains three crates; from bottom to top, they are crates `M`, `C`, and `D`. Finally, stack 3 contains a single crate, `P`.

Then, the rearrangement procedure is given. In each step of the procedure, a quantity of crates is moved from one stack to a different stack. In the first step of the above rearrangement procedure, one crate is moved from stack 2 to stack 1, resulting in this configuration:

```
[D]        
[N] [C]    
[Z] [M] [P]
 1   2   3 
```

In the second step, three crates are moved from stack 1 to stack 3. Crates are moved one at a time, so the first crate to be moved (`D`) ends up below the second and third crates:

```
        [Z]
        [N]
    [C] [D]
    [M] [P]
 1   2   3
```

Then, both crates are moved from stack 2 to stack 1. Again, because crates are moved one at a time, crate C ends up below crate M:

```
        [Z]
        [N]
[M]     [D]
[C]     [P]
 1   2   3
```

Finally, one crate is moved from stack 1 to stack 2:

```
        [Z]
        [N]
        [D]
[C] [M] [P]
 1   2   3
```

The Elves just need to know __which crate will end up on top of each stack__; in this example, the top crates are `C` in stack 1, `M` in stack 2, and `Z` in stack 3, so you should combine these together and give the Elves the message `CMZ`.

__After the rearrangement procedure completes, what crate ends up on top of each stack?__

```elixir
defmodule Instructions do
  @doc """
  Pulls the stack id from the back of the stack.

  ## Examples

    iex> Instructions.index(['G', 'Y', '1'])
    1
  """
  def index(stack) when is_list(stack), do: List.last(stack) |> String.to_integer()

  @doc """
  ## Examples

    iex> Instructions.parse_action_line("moves 3 from 5 to 1")
    [3, 5, 1]
  """
  def parse_action_line(line) do
    [[a], [f], [t]] = Regex.scan(~r/\d+/, line)
    [String.to_integer(a), String.to_integer(f), String.to_integer(t)]
  end

  @doc """
  ## Examples

    iex> Instructions.parse_stack_line("[G]     [Y]")
    ["G", " ", "Y"]
  """
  def parse_stack_line(line) do
    line
    |> String.graphemes()
    |> Enum.chunk_every(4)
    |> Enum.map(&Enum.at(&1, 1))
  end

  @doc """
  ## Examples

    iex> Instructions.pop_empty([" ", " ", "G", "Y"])
    ["G", "Y"]
  """
  def pop_empty([head | tail]) when head == " ", do: Instructions.pop_empty(tail)
  def pop_empty(stack), do: stack
end

defmodule Operator do
  def move(actions, stacks, crane) do
    Enum.reduce(actions, stacks, &crane.move/2)
    |> Map.values()
    |> Enum.map(&hd(&1))
    |> Enum.join()
  end
end

[stacks, actions] =
  File.read!("workspaces/advent-of-code/2022/inputs/05.txt")
  |> String.split("\n\n", trim: true)

stacks =
  stacks
  |> String.split("\n")
  |> Enum.map(&Instructions.parse_stack_line/1)
  |> List.zip()
  |> Enum.map(&Tuple.to_list/1)
  |> Enum.map(fn stack -> Instructions.pop_empty(stack) end)
  |> Map.new(fn x -> {Instructions.index(x), x} end)

actions =
  actions
  |> String.split("\n", trim: true)
  |> Enum.map(&Instructions.parse_action_line/1)
```

```elixir
defmodule CrateMover9000 do
  def move([amount, _from, _to], stack) when amount == 0, do: stack

  def move([amount, from, to], stack) do
    [head | _tail] = Map.get(stack, from)

    stack
    |> Map.update!(from, fn [_head | tail] -> tail end)
    |> Map.update!(to, fn s -> [head | s] end)
    |> then(&move([amount - 1, from, to], &1))
  end
end

Operator.move(actions, stacks, CrateMover9000)
```

As you watch the crane operator expertly rearrange the crates, you notice the process isn't following your prediction.

Some mud was covering the writing on the side of the crane, and you quickly wipe it away. The crane isn't a CrateMover 9000 - it's a CrateMover 9001.

The CrateMover 9001 is notable for many new and exciting features: air conditioning, leather seats, an extra cup holder, and the ability to pick up and move multiple crates at once.

Again considering the example above, the crates begin in the same configuration:

```
    [D]    
[N] [C]    
[Z] [M] [P]
 1   2   3 
```

Moving a single crate from stack 2 to stack 1 behaves the same as before:

```
[D]        
[N] [C]    
[Z] [M] [P]
 1   2   3 
```

However, the action of moving three crates from stack 1 to stack 3 means that those three moved crates stay in the same order, resulting in this new configuration:

```
        [D]
        [N]
    [C] [Z]
    [M] [P]
 1   2   3
```

Next, as both crates are moved from stack 2 to stack 1, they retain their order as well:

```
        [D]
        [N]
[C]     [Z]
[M]     [P]
 1   2   3
```

Finally, a single crate is still moved from stack 1 to stack 2, but now it's crate C that gets moved:

```
        [D]
        [N]
        [Z]
[M] [C] [P]
 1   2   3
```

In this example, the CrateMover 9001 has put the crates in a totally different order: MCD.

Before the rearrangement process finishes, update your simulation so that the Elves know where they should stand to be ready to unload the final supplies. __After the rearrangement procedure completes, what crate ends up on top of each stack?__

```elixir
defmodule CrateMover9001 do
  def move([amount, _from, _to], stack) when amount == 0, do: stack

  def move([amount, from, to], stack) do
    [head | _tail] = Map.get(stack, from)

    stack
    |> Map.update!(from, fn [_head | tail] -> tail end)
    |> then(&move([amount - 1, from, to], &1))
    |> Map.update!(to, fn s -> [head | s] end)
  end
end

Operator.move(actions, stacks, CrateMover9001)
```
